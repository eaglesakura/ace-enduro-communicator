apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.eaglesakura.android-support'

configurations {
    apt

    all*.exclude group: "com.google.code.findbugs", module: "jsr305"
    all*.exclude group: "com.eaglesakura", module: "eglibrary-java-geo"
    all*.exclude group: "com.eaglesakura", module: "eglibrary-android-java-core"
}

dependencies {
    compile project(':eglibrary-java-geo')
    compile project(':eglibrary-android-framework')
    compile project(':ace-sdk')

    // Android Annotations
    apt "org.androidannotations:androidannotations:3.0.+"
    compile 'org.androidannotations:androidannotations-api:3.0.+'
}

if (!hasProperty("DEBUG_KEYSTORE_PASS")) {
    ext.DEBUG_KEYSTORE_PASS = ""
    ext.DEBUG_KEYSTORE_ALIAS_NAME = ""
    ext.DEBUG_KEYSTORE_ALIAS_PASS = ""
    ext.RELEASE_KEYSTORE_PASS = ""
    ext.RELEASE_KEYSTORE_ALIAS_NAME = ""
    ext.RELEASE_KEYSTORE_ALIAS_PASS = ""
}


android {
    compileSdkVersion 22
    buildToolsVersion '23.0.0'

    defaultConfig {
        applicationId "com.eaglesakura.andriders.communication"
        minSdkVersion 18
        targetSdkVersion 22
        versionCode eglibrary.ci.buildVersionCode as int;
        // 0.1.x : 初期バージョン for GSR cup
        versionName "0.1.${eglibrary.ci.buildVersionCode}";
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src/main/java']
        }
    }

    // 署名設定
    signingConfigs {
        debug {
            storeFile file('../sign/appdebug.jks')
            storePassword DEBUG_KEYSTORE_PASS
            keyAlias DEBUG_KEYSTORE_ALIAS_NAME
            keyPassword DEBUG_KEYSTORE_ALIAS_PASS
        }

        release {
            storeFile file('../sign/apprelease.jks')
            storePassword RELEASE_KEYSTORE_PASS
            keyAlias RELEASE_KEYSTORE_ALIAS_NAME
            keyPassword RELEASE_KEYSTORE_ALIAS_PASS
        }
    }

    buildTypes {
        debug {
            // デバッグビルド時は"package名+.debug"を入れる
            applicationIdSuffix ".debug"
            // デバッグ版署名
            signingConfig signingConfigs.debug

            minifyEnabled false
        }

        release {
            minifyEnabled false
            // 署名設定
            signingConfig signingConfigs.release
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'androidannotations-api.properties'
        exclude 'services/com.fasterxml.jackson.core.JsonFactory'
        exclude 'services/com.fasterxml.jackson.core.ObjectCodec'
    }
}
